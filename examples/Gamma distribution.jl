"""

**Short description:**

- SMARTboost with gamma distribution on simulated data (generated by a gamma distribution).
- The shape parameter k is estimated internally.
- loss=:gamma clearly outperforms loss=:L2 in terms of out-of-sample RMSE, 
  but :L2loglink approaches the performance of :gamma and is more robust if the density is not gamma.

Note: The comparison with LightGBM is biased toward SMARTboost if the function generating the data is 
smooth in some features (this is easily changed by the user). lightGBM is cross-validated over max_depth and num_leaves,
with the number of trees set to 1000 and found by early stopping.
  
"""

number_workers  = 8  # desired number of workers

using Distributed
nprocs()<number_workers ? addprocs( number_workers - nprocs()  ) : addprocs(0)
@everywhere using SMARTboostPrivate

using Random,Plots,Distributions 
using LightGBM

# USER'S OPTIONS 

Random.seed!(2)

# Some options for SMARTboost
loss      = :gamma         
modality  = :compromise   # :accurate, :compromise (default), :fast, :fastest 

priortype = :hybrid       # :hybrid (default) or :smooth to force smoothness 
nfold     = 1             # number of cv folds. 1 faster (single validation sets), default 5 is slower, but more accurate.
nofullsample = true       # if nfold=1 and nofullsample=true, the model is not re-fitted on the full sample after validation of the number of trees
 
randomizecv = false       # false (default) to use block-cv. 
verbose     = :Off
warnings    = :On

# options to generate data.
k           = 5         # shape parameter of the gamma distribution 
n,p,n_test  = 10_000,4,100_000   # sample size and number of features

f_1(x,b)    = b*x  
f_2(x,b)    = -b*(x.<0.5) + b*(x.>=0.5)   
f_3(x,b)    = b*x
f_4(x,b)    = -b*(x.<0.5) + b*(x.>=0.5)

b1,b2,b3,b4 = 0.2,0.2,0.2,0.2

# generate data
x,x_test = randn(n,p), randn(n_test,p)

c        = -2  
f        = c .+ f_1(x[:,1],b1) + f_2(x[:,2],b2) + f_3(x[:,3],b3) + f_4(x[:,4],b4)
f_test   = c .+ f_1(x_test[:,1],b1) + f_2(x_test[:,2],b2) + f_3(x_test[:,3],b3) + f_4(x_test[:,4],b4)

μ        = exp.(f)        # conditional mean 
μ_test   = exp.(f_test)   # conditional mean 

scale      = μ/k
scale_test = μ_test/k
y          = zeros(n)

for i in eachindex(y)
    y[i]  = rand(Gamma.(k,scale[i]))
end 

histogram(y)
@show [mean(y), std(y), std(μ), maximum(y)]

# set up SMARTparam and SMARTdata, then fit and predit

# coefficient estimated internally. 
param  = SMARTparam(loss=loss,priortype=priortype,randomizecv=randomizecv,nfold=nfold,
                   verbose=verbose,warnings=warnings,modality=modality,nofullsample=nofullsample)
data   = SMARTdata(y,x,param)

output = SMARTfit(data,param)
yf     = SMARTpredict(x_test,output,predict=:Ey)

println(" \n loss = $loss, modality = $(param.modality), nfold = $nfold ")
println(" depth = $(output.bestvalue), number of trees = $(output.ntrees) ")
println(" out-of-sample RMSE from truth, μ     ", sqrt(sum((yf - μ_test).^2)/n_test) )

println("\n true shape = $k, estimated = $(exp(output.bestparam.coeff_updated[1][1])) ")
println("\n For more information about coefficients, use SMARTcoeff(output) ")
SMARTcoeff(output)


# Repeat for :L2 or :L2loglink loss

param_L2 = deepcopy(param) 
param_L2.loss = :L2loglink         # :L2loglink should outperform :L2 with these data
output_L2 = SMARTfit(data,param_L2)
yf    = SMARTpredict(x_test,output_L2,predict=:Ey)  

println(" \n loss = $(param_L2.loss), modality = $(param.modality), nfold = $nfold")
println(" depth = $(output_L2.bestvalue), number of trees = $(output_L2.ntrees) ")
println(" out-of-sample RMSE from truth, μ      ", sqrt(sum((yf - μ_test).^2)/n_test) )

# lightGBM

estimator = LGBMRegression(
    objective = "gamma",
    metric = ["gamma"],        # default seems (strangely) "l2" regardless of objective: LightGBM.jl bug?  
    num_iterations = 1000,
    learning_rate = 0.1,
    early_stopping_round = 100,
    num_threads = number_workers,
    max_depth = 6,      # -1 default
    min_data_in_leaf = 100,  # 100 default 
    num_leaves = 127         # 127 default  
)

# Fit lightGBM 

n_train = Int(round((1-param.sharevalidation)*length(y)))
x_train = x[1:n_train,:]; y_train = Float64.(y[1:n_train])
x_val   = x[n_train+1:end,:]; y_val = Float64.(y[n_train+1:end])
    
# fit at parameters given by estimator, no cv
LightGBM.fit!(estimator,x_train,y_train,(x_val,y_val),verbosity=-1)
    
yf_gbm_default = LightGBM.predict(estimator,x_test)[:,1]

# parameter search over num_leaves and max_depth
splits = (collect(1:n_train),collect(1:min(n_train,100)))  # goes around the problem that at least two training sets are required by search_cv (we want the first)

params = [Dict(:num_leaves => num_leaves,
               :max_depth => max_depth) for
          num_leaves in (4,16,32,64,127,256),
          max_depth in (2,3,5,6,8)]

lightcv = LightGBM.search_cv(estimator,x,y,splits,params,verbosity=-1)

loss_cv = [lightcv[i][2]["validation"][estimator.metric[1]][1] for i in eachindex(lightcv)]
minind = argmin(loss_cv)

estimator.num_leaves = lightcv[minind][1][:num_leaves]
estimator.max_depth  = lightcv[minind][1][:max_depth]

# re-fit at cv parameters
LightGBM.fit!(estimator,x_train,y_train,(x_val,y_val),verbosity=-1)
    
yf_gbm = LightGBM.predict(estimator,x_test)
yf_gbm = yf_gbm[:,1]    # drop the second dimension or a (n_test,1) matrix 

println("\n oss RMSE from truth, μ, LightGBM default ", sqrt(sum((yf_gbm_default - μ_test).^2)/n_test) )
println(" oss RMSE from truth, μ, LightGBM cv      ", sqrt(sum((yf_gbm - μ_test).^2)/n_test) )

# Plot 

q,pdp  = SMARTpartialplot(data,output,[1,2,3,4],predict=:Egamma)

# plot partial dependence in terms of the natural parameter 
pl   = Vector(undef,4)
f,b  = [f_1,f_2,f_3,f_4],[b1,b2,b3,b4]

for i in 1:length(pl)
        pl[i]   = plot( [q[:,i]],[pdp[:,i] f[i](q[:,i],b[i]) - f[i](q[:,i]*0,b[i])],
           label = ["smart" "dgp"],
           legend = :bottomright,
           linecolor = [:blue :red],
           linestyle = [:solid :dot],

           linewidth = [5 5],
           titlefont = font(15),
           legendfont = font(12),
           xlabel = "x",
           ylabel = "f(x)",
           )
end

display(plot(pl[1], pl[2], pl[3], pl[4], layout=(2,2), size=(1300,800)))  # display() will show it in Plots window.
